= Managing Releases

With the `helm-releases` plugin, you can manage Helm releases on a remote Kubernetes cluster.

It allows you to define your releases in the Gradle DSL, in a declarative fashion. In this way, it can
be used as an alternative to tools like https://github.com/roboll/helmfile[Helmfile] or
https://github.com/Praqma/Helmsman[Helmsman], with the advantage of leveraging the full power of Gradle
instead of defining a custom file format.


Apply the `helm-releases` plugin to your project:

[source,groovy,role="primary",subs="+attributes"]
.Groovy
----
plugins {
    id 'com.citi.helm-releases' version '{project-version}'
}
----

[source,kotlin,role="secondary",subs="+attributes"]
.Kotlin
----
plugins {
    id("com.citi.helm-releases") version "{project-version}"
}
----


Define your releases using the `helm.releases` block in your Gradle script:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releases {
        mariadb {
            from 'stable/mariadb'
            version = '5.1.1'

            // pass values (like --set on the command line)
            values = [ 'rootUser.password': 'secret' ]

            // pass value files (like -f on the command line)
            valueFiles.from 'mariadb.yaml'
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releases {
        create("mariadb") {
            from("stable/mariadb")
            version.set("5.1.1")

            // pass values (like --set on the command line)
            values.set(mapOf("rootUser.password" to "secret"))

            // pass value files (like -f on the command line)
            valueFiles.from("mariadb.yaml")
        }
    }
}
----

There are quite a few properties you can set for a release; most of them correspond to a command line
option in `helm install`, `helm upgrade` or `helm uninstall`.

The `from` method is quite powerful, as it accepts various sources for the chart from which the release
will be created. Besides a `String` (for specifying the chart directly), it can also be a `File`,
`RegularFile`, `Directory`, `URI` or also a Gradle `Provider` of any of these types.

It is also possible to use a `FileCollection` (e.g. a Gradle `Configuration`), which should consist of
only one file. In that case, any
https://docs.gradle.org/current/javadoc/org/gradle/api/Buildable.html#getBuildDependencies--[build dependencies]
expressed by the `FileCollection` will be honored by the release.

Of course you can also reference charts built by the `helm` plugin, by just passing the chart's DSL object
to `from`:

[source,groovy,role="primary"]
.Groovy
----
helm {
    charts {
        foo {
            // configure the foo chart ...
        }
    }

    releases {
        foo {
            from charts.foo
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    val foo by charts.creating {
        // configure the foo chart ...
    }

    releases {
        create("foo") {
            from(foo)
        }
    }
}
----

You can also refer to a chart by name (and optionally project) by using the `chart` helper function:

[source,groovy,role="primary"]
.Groovy
----
// Chart in the same project, equivalent to charts.foo
from chart('foo')

// foo chart in the foo project
from chart(project: ':foo', chart: 'foo')

// main chart in the foo project
from chart(project: ':foo')
----

[source,kotlin,role="secondary"]
.Kotlin
----
// Chart in the same project, equivalent to charts["foo"]
from(chart("foo"))

// foo chart in the foo project
from(chart(project = ":foo", chart = "foo"))

// main chart in the foo project
from(chart(project = ":foo"))
----


== Release Tasks

For each release defined in the `releases` block, the following Gradle tasks will be generated:

Task `helmInstall<X>`::
Installs the release named X. This task will also do upgrades; depending on the `replace` property
it will either call `helm upgrade --install` (by default) or `helm install --replace`.

Task `helmUninstall<X>`::
Uninstalls the release named X (by calling `helm uninstall`).


In addition, there will be the following tasks to manage _all_ releases in the project at once:

Task `helmInstall`::
Install or upgrade all releases.

Task `helmUninstall`::
Uninstall all releases configured in the build script.

TIP: If you use a chart built by the helm plugin for a release, the corresponding `helmInstall`
task will have a task dependency on the `helmPackage` task so that the chart is guaranteed to be
up to date before it is installed.


[[release-installation-order]]
== Release Installation Order

You can influence the order in which releases are installed or uninstalled by calling `mustInstallAfter`
on the release. This is similar to what the `mustRunAfter` method from the Gradle DSL does for tasks:
`mustInstallAfter` does not express a "hard" dependency on another release; instead it only influences
the order in which their install tasks are called when both are requested to be installed in the current
Gradle invocation.

Currently it is not possible to refer to releases in another Gradle project.

TIP: While `mustInstallAfter` influences the order of `helm install` invocations, it does not guarantee
that the release will be up and running on the cluster when the installation of the dependent release
begins. By default, `helm install` does not wait until the deployment is complete -- if this is what
you need, you can set `wait` to `true` in the release, so that the install/upgrade command is invoked
with the `--wait` flag.

[source,groovy,role="primary"]
.Groovy
----
helm.releases {

    postgres {
        from 'stable/postgresql'

        // Set the wait property to true if the following release
        // requires this to be successfully deployed
        wait = true

        // You can set the waitForJobs property in addition to wait,
        // so we will also wait for all hook jobs to be completed
        waitForJobs = true
    }

    myApp {
        // ...
        mustInstallAfter 'postgres'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases {

    create("postgres") {
        from("stable/postgresql")

        // Set the wait property to true if the following release
        // requires this to be successfully deployed
        wait.set(true)

        // You can set the waitForJobs property in addition to wait,
        // so we will also wait for all hook jobs to be completed
        waitForJobs.set(true)
    }

    create("myApp") {
        // ...
        mustInstallAfter("postgres")
    }
}
----

Similarly, influencing the order of uninstalls is also possible using the `mustUninstallAfter` method. Note that
`mustInstallAfter` and `mustUninstallAfter` are completely independent; neither of them implies the other. If you
want to express both installation and uninstallation ordering between two releases, you must do so explicitly by
calling both `mustInstallAfter` and `mustUninstallAfter`:

[source,groovy,role="primary"]
.Groovy
----
helm.releases {

    postgres {
        from 'stable/postgresql'

        // Set the wait property to true if the following release
        // requires this to be successfully deployed
        wait = true

        // We can refer to a release by name even before it is declared --
        // the references are resolved lazily
        mustUninstallAfter 'myApp'
    }

    myApp {
        // ...
        mustInstallAfter 'postgres'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases {

    create("postgres") {
        from("stable/postgresql")

        // Set the wait property to true if the following release
        // requires this to be successfully deployed
        wait = true

        // We can refer to a release by name even before it is declared --
        // the references are resolved lazily
        mustUninstallAfter("myApp")
    }

    create("myApp") {
        // ...
        mustInstallAfter("postgres")
    }
}
----


== Release Dependencies

[NOTE]
====
This feature is now deprecated as of version 1.2.0, and will removed in a future version. Instead, please use
<<using-tags-to-select-releases,tags>> to specify which releases to install, and
<<release-installation-order,Release Installation Order>> to indicate in which order they should be installed.

Release dependencies are now deprecated because the side effects of automatically installing or uninstalling a
dependent release may often be undesirable. If the dependent release is already installed, then it would always
be upgraded even if that is not necessary. Likewise, the automatic uninstallation is not always desired and should
be controlled in a more fine-grained way using the other mechanisms documented here.
====

The `dependsOn` property and method on a release allows you to indicate that a certain release depends on another
release. As a consequence, when release A depends on release B, then release B will automatically installed before
release A is installed, and uninstalled after release A is uninstalled.

[source,groovy,role="primary"]
.Groovy
----
helm.releases {

    postgres {
        from 'stable/postgresql'
    }

    myApp {
        // myApp depending on postgres means that whenever myApp is installed,
        // postgres will be automatically installed first; and whenever myApp
        // is uninstalled, postgres will be uninstalled afterwards.
        dependsOn 'postgres'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases {

    create("postgres") {
        from("stable/postgresql")
    }

    create("myApp") {
        // myApp depending on postgres means that whenever myApp is installed,
        // postgres will be automatically installed first; and whenever myApp
        // is uninstalled, postgres will be uninstalled afterwards.
        dependsOn("postgres")
    }
}
----


== Install/Upgrade logic

The installation task that the plugin creates for each release, named `helmInstall<Name>Release`, will perform an
install or upgrade based on the following logic:

* if the release has the `replace` property set to `true`, it will always call `helm install --replace`
* Otherwise, it calls `helm ls` as an intermediate step to determine the current status of the release.
** if the release exists but previously failed, it will call `helm install --replace`. This works around the
  `UPGRADE FAILED: "<name>" has no deployed releases` issue if a previous call to `helm install` was not successful.
** Otherwise, it will call `helm upgrade --install` (also if the release does not exist).


== Installation Task Dependencies

Sometimes it is necessary to execute other task dependencies before a release can be installed. For example, when
using Helm and link:https://plugins.gradle.org/plugin/com.google.cloud.tools.jib[jib] together, you may want to
call the `jib` task that builds your Docker image before you install the release.

You can declare such dependencies using the `installDependsOn` property or method on the release:

[source,groovy,role="primary"]
.Groovy
----
helm.releases {

    myApp {
        // ...
        installDependsOn 'jib'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases {

    create("myApp") {
        // ...
        installDependsOn("jib")
    }
}
----


== Using Release Targets

A frequent requirement when using Helm is to designate different _release targets_, with target-specific configuration
applied to each chart depending on the target. For example, you might use release targets to model different
server environments, stages of development (e.g. dev / test / production), or different variants of installation.

=== Defining Release Targets

Use the `helm.releaseTargets` container in the Gradle build script to add release targets. For example, you might
want to use a different `kubeContext` for each target, and use the `--atomic` flag only for production installs:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releaseTargets {
        local {
            kubeContext = 'docker-for-desktop'
        }
        production {
            kubeContext = 'aws'
            atomic = true
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releaseTargets {
        create("local") {
            kubeContext.set("docker-for-desktop")
        }
        create("production") {
            kubeContext.set("aws")
            atomic.set(true)
        }
    }
}
----

INFO: If you don't create your own release targets, the plugin will create a release target named `default` that
uses all the default properties. As soon as you create other release targets, the `default` target will back away.

Values (and related properties) can be added to a release target as well, and they will be used for _every_ release
that is installed to this target:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releaseTargets {
        local {
            values.put('replication.enabled', false)
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releaseTargets {
        create("local") {
            values.put("replication.enabled", false)
        }
    }
}
----


When a release is installed to a target, the parameters for the `helm install` / `helm upgrade` call are determined
from the properties of both the release and the release target. If a property is defined on both the release and the
release target, the release has precedence (except for values, which will be merged from both sources).


=== Installing to and Uninstalling From a Specific Target

For each Gradle run, one of the release targets will be the _active_ release target. This can be controlled by setting
the `helm.release.target` property:

[source,bash]
----
./gradlew helmInstall -Phelm.release.target=production
----

It is also useful to set a default value for this property in your `gradle.properties` file, to indicate a default
release target that can be selectively overridden on the command line:

[source]
.gradle.properties
----
helm.release.target=local
----

TIP: For each Gradle build with the plugin, there can only ever be one active release target. You cannot install
charts to multiple targets from within the same build. Even though the plugin registers tasks like
`helmInstallMyReleaseToLocal` even for inactive targets, those will be SKIPPED when part of the task execution graph.


=== Target-Specific Release Configuration

Inside a `release`, you can add a `forTarget { }` block that applies configuration only for a specific target.
For example, many Helm charts can be configured to create a `Secret` or use an existing secret:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releaseTargets {
        local
        production
    }

    releases {
        myApp {
            from 'my-repo/my-application'
            forTarget('local') {
                values.put('auth.username', 'username')
                values.put('auth.password', 'password')
            }
            forTarget('production') {
                values.put('auth.existingSecret', 'some-existing-secret')
            }
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releaseTargets {
        create("local")
        create("production")
    }

    releases {
        create("myApp") {
            from("my-repo/my-application")
            forTarget("local") {
                values.put("auth.username", "username")
                values.put("auth.password", "password")
            }
            forTarget("production") {
                values.put("auth.existingSecret", "some-existing-secret")
            }
        }
    }
}
----

TIP: If you prefix the argument to `forTarget` with an exclamation mark (e.g. `forTarget("!local")`), the block
will be evaluated for all targets _but_ the given one.

`forTarget` blocks are evaluated lazily when the release is actually installed to the given target. You can modify
most of the properties of the `release` in the `forTarget` block. For example, to add additional installation task
dependencies based on the target, call `installDependsOn` inside a `forTarget` block:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releases {
        myApp {
            from 'my-repo/my-application'
            forTarget('local') {
                installDependsOn 'jibDockerBuild'
            }
            forTarget('production') {
                installDependsOn 'jib'
            }
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releases {
        create("myApp") {
            from("my-repo/my-application")
            forTarget("local") {
                installDependsOn("jibDockerBuild")
            }
            forTarget("!local") {
                installDependsOn("jib")
            }
        }
    }
}
----

If you need to apply dynamic target-specific configuration that depends on the target name, the scope of each
`forTarget` block contains a `target` property that allows access to the target for which the release is being
configured. In the case where this `target` reference is all you need, you could even use `forAnyTarget`, which
is always called.

For example, to pass a specific file to `fileValues`, whose name depends on the release target name,
you could do this:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releases {
        myApp {
            from 'my-repo/my-application'
            forAnyTarget {
                fileValues.put('tls.certificate', "cert/cert-${target.name}.pem")
            }
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releases {
        create("myApp") {
            from("my-repo/my-application")
            forAnyTarget {
                fileValues.put("tls.certificate", "cert/cert-${target.name}.pem")
            }
        }
    }
}
----

TIP: For more advanced scenarios, both the release and the release target DSL objects are `ExtensionAware`, which
means they have an extra properties extension where you can store custom properties that can then be accessed
in a `forTarget` or `forAnyTarget` block.


==== Directories of Value Files

Since values and value files are the most common thing to customize per target, there exists another handy mechanism
which allows you to declare a `valuesDir` directory containing YAML files with filenames according to a specific
convention. Assuming you have a directory containing value files like this:

----
ðŸ“‚ myapp-values
  ðŸ“„ values.yaml
  ðŸ“„ values-local.yaml
  ðŸ“„ values-production.yaml
----

Pass the path of the directory to the release by calling `valuesDir`:

[source,groovy,role="primary"]
.Groovy
----
helm {
    releases {
        myApp {
            from 'my-repo/my-application'
            valuesDir 'myapp-values'
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releases {
        create("myApp") {
            from("my-repo/my-application")
            valuesDir("myapp-values")
        }
    }
}
----

When installing a release to a given target, the `values-<target>.yaml` file is automatically passed to the
`helm install` or `helm upgrade` command. If the directory contains a `values.yaml` file (without a suffix), then it
will be used for all targets, but with a lower precedence than the target-specific values files, meaning you can
define common defaults in `values.yaml` and then selectively override them for each target.

Each of the files in the values directory is optional, if a certain file name pattern does not exist, it will not
be used.

In the above example, when installing `myApp` to the `local` target, it would call `helm install` / `helm upgrade`
with the option `--values myapp-values/values.yaml,myapp-values/values-local.yaml`. Similarly, when installing to the
`production` target, it would use the option `--values myapp-values/values.yaml,myapp-values/values-production.yaml`.

[TIP]
====
Use the built-in Gradle mechanisms to automatically declare a `valuesDir` for each release according to your own
convention. Assuming your directory structure contains value files for each release in a subdirectory
`$projectDir/helm-values/<release-name>`:

[source,groovy,role="primary"]
.Groovy
----
helm.releases.all {
    valuesDir "helm-values/$name"
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases.all {
    valuesDir("helm-values/$name")
}
----
====


[[using-tags-to-select-releases]]
== Using Tags to Select Releases

Often you want to declare multiple releases in your build script, but you want to selectively install only a subset
of them. For example, when installing a set of applications in a local cluster, you might want to include infrastructure
components like a database or message queue as well, but exclude them when installing to production because they can
be assumed to be managed externally.

The `helm-releases` plugin offers a powerful tagging mechanism for such setups:

* Each release may be assigned a set of tags
* A release target may contain a selection expression to use only a subset of releases
* A global selection expression allows you to further filter the set of releases per-build

In the following example, we create two releases `myApp` and `mongodb`, and assign them tags `application` and
`database`, respectively:

[source,groovy,role="primary"]
.Groovy
----
helm.releases {
    myApp {
        from 'my-repo/my-application'
        tags 'application'
    }
    mongodb {
        from 'bitnami/mongodb'
        tags 'database'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releases {
    create("myApp") {
        from("my-repo/my-application")
        tags("application")
    }
    create("mongodb") {
        from("bitnami/mongodb")
        tags("database")
    }
}
----

Now we can specify a select expression in our release targets to configure which releases should be installed to
each target. In its simplest form, the select expression is just the name of a tag, which must then be present on
the release. The expression `*` (which is also the default) matches any release.

[source,groovy,role="primary"]
.Groovy
----
helm.releaseTargets {
    local {
        // this is the default
        selectTags = '*'
    }
    production {
        // When installing to production, only install releases tagged "application"
        selectTags = 'application'
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm.releaseTargets {
    create("local") {
        // this is the default
        selectTags = "*"
    }
    create("production") {
        // When installing to production, only install releases tagged "application"
        selectTags = "application"
    }
}
----

You can combine tags in a select expression using the operators `&` (and), `|` (or) and `!` (not). Comma and space also
mean "or". Some examples:

* `!database` matches any release except ones tagged with `database`
* `application,infrastructure` matches any release tagged `application` _or_ `infrastructure`
* `infrastructure & !database` matches any release tagged `infrastructure`, but not tagged `database`

In addition to setting `selectTags` on a release target, you can also set the property `helm.release.tags` on the
command line to further narrow down the set of releases per-build.

[source,bash]
----
# this will install only "application"-tagged releases to the "local" target
./gradlew helmInstall -Phelm.release.target=local -Phelm.release.tags=application
----

INFO: If both `helm.release.tags` and the active release target contain a select expression for tags, they will be
both combined using "and". This means that a release will only be installed if it matches _both_ the `helm.release.tags`
expression and the `selectTags` expression of the release target.


== Testing Releases

Helm includes a facility for testing charts after they have been installed on a remote cluster. Tests are Kubernetes
`Job` resources with an annotation `helm.sh/hook: test`. See the
link:https://docs.helm.sh/docs/topics/chart_tests/[Chart Tests] section in the Helm documentation for details.

The `helm-releases` plugin integrates chart testing by exposing a `HelmTest` task for every release that is declared
in the build script. Like other release options, tests can be configured (or even completely disabled) for a release,
a release target, or a specific combination of the two.

=== Release Testing Tasks

For each release defined in the `releases` block, the following Gradle task will be generated:

Task `helmTest<X>`::
Tests the release named X on the active release target (as indicated by `-Phelm.release.target`)
by calling `helm test <X>`.


In addition, there will be the following tasks to test _all_ releases in the project at once:

Task `helmTest`::
Test all releases (except those that have the `enabled` property set to `false`).

TIP: If your chart does not contain any tests, `helm test` (and therefore the `HelmTest` task) will simply do nothing.


A `helmTest<X>` task created for a release will _not_ automatically have a task dependency on the corresponding
`helmInstall<X>` task. (It does have a `mustRunAfter` relationship, however.) This means that simply calling
`gradle helmTest<X>` will not automatically install the release. This allows you to use the `helmTest` or `helmTest<X>`
tasks even for testing releases that are already deployed.

If you want to install and test releases in the same Gradle build, then you need to include both `helmInstall` and
`helmTest` in the Gradle invocation, e.g.

[source,bash]
----
gradle helmInstall helmTest
----

In this case, you should also set the `wait` property to `true` on the release, to ensure that the release
is up and running before the test starts.


=== Release Testing Configuration

You can fine-tune testing options using the `test` DSL block inside a release or release target.

[source,groovy,role="primary"]
.Groovy
----
helm {
    releases {
        foo {
            from 'my-repo/foo'
            test {
                // dump test logs
                showLogs = true
            }
        }
        bar {
            from 'my-repo/bar'
            test {
                // disable testing for this release (enabled by default)
                enabled = false
            }
        }
    }

    releaseTargets {
        local {
            test {
                // always dump test logs for this target
                showLogs = true
            }
        }
        remote {
            test {
                // configure a different timeout for this target
                timeout = Duration.ofSeconds(10)
            }
        }
    }
}
----

[source,kotlin,role="secondary"]
.Kotlin
----
helm {
    releases {
        create("foo") {
            from("my-repo/foo")
            test {
                // dump test logs
                showLogs.set(true)
            }
        }
        create("bar") {
            from("my-repo/bar")
            test {
                // disable testing for this release (enabled by default)
                enabled.set(false)
            }
        }
    }

    releaseTargets {
        create("local") {
            test {
                // always dump test logs for this target
                showLogs.set(true)
            }
        }
        create("remote") {
            test {
                // configure a different timeout for this target
                timeout.set(Duration.ofSeconds(10))
            }
        }
    }
}
----

The `showLogs` property (which corresponds to the `--logs` flag to the `helm test` CLI command) can also be configured
globally on a per-invocation basis by setting the `helm.test.logs` property:

[source,bash]
----
gradle helmTest -Phelm.test.logs=true
----


== Checking the Status of a Release

For each release/target combination, the `helm-releases` plugin will provide a task named
`helmStatus<Release>On<Target>` to check the status of the release. Internally, this will map to a call of
`helm status <release-name>`.

For example, to check the status of the "awesome" release on the "local" target:

[source,bash]
----
gradle helmStatusAwesomeOnLocal
----

In addition, you can also check the status of a release on the active target (as indicated by the `helm.release.target`
project property) using the `helmStatus<Release>` task:

[source,bash]
----
gradle helmStatusAwesome -Phelm.release.target=local
----

=== Controlling the output file and format

`helm status` offers an `--output` option that allows you to select the desired output format for the status report.
With the `helm-releases` plugin, this can be achieved by setting the `helm.status.outputFormat` property:

[source,bash]
----
gradle helmStatusAwesome -Phelm.status.outputFormat=yaml
----

You can also output the status report to a file instead of stdout, by setting the `helm.status.outputFile` property:

[source,bash]
----
gradle helmStatusAwesome -Phelm.status.outputFile='helm-status.json'
----

Relative paths are resolved from the project directory of the `HelmStatus` task, which might not be the root directory.
You can use Groovy GString expansion in the property value to force it to the root directory (but remember to
properly quote "`$`" characters when calling from a shell):

[source,bash]
----
gradle helmStatusAwesome -Phelm.status.outputFile='$rootDir/build/helm-status.json'
----

TIP: If an output file is used, and the output format is not explicitly set, the correct format is automatically
guessed based on the file extension. For example, for an output file with the `.json` extension, the output format
defaults to `json` (instead of `table`, which is Helm's default).
